apply plugin: 'com.android.application'

android {
    compileSdkVersion redditLiteCompileSdkVersion
    defaultConfig {
        applicationId "com.sriky.redditlite"
        minSdkVersion redditLiteMinSdkVersion
        targetSdkVersion redditLiteTargetSdkVersion
        versionCode 100
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    /* gather required ids */
    buildTypes.each{
        it.buildConfigField "String", 'REDDIT_CLIENT_ID', getPropertyFromFile("reddit_client_id")
        it.buildConfigField "String", 'REDDIT_REDIRECT_URL', getPropertyFromFile("reddit_redirect_url")
        it.buildConfigField "String", 'REDDIT_USERNAME', getPropertyFromFile("reddit_username")
    }

    /* enable data binding */
    dataBinding.enabled = true;
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /* Reddit API Client lib */
    implementation 'com.github.mattbdean:JRAW:master-SNAPSHOT'

    /* AOSP libs */
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:support-annotations:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support.test.espresso:espresso-idling-resource:${espressoLibVersion}"

    /* Timber dependencies used for logging */
    implementation 'com.jakewharton.timber:timber:4.6.0'

    /* Schematic dependencies for ContentProvider */
    annotationProcessor 'net.simonvt.schematic:schematic-compiler:0.7.0'
    implementation 'net.simonvt.schematic:schematic:0.7.0'

    /* Firebase JobDispatcher */
    implementation 'com.firebase:firebase-jobdispatcher:0.5.2'

    /* Event Bus */
    implementation 'org.greenrobot:eventbus:3.1.1'

    /* About screen helper */
    implementation('com.mikepenz:aboutlibraries:6.0.0@aar') {
        transitive = true
    }

    /* GSON with Retrofit */
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.0.2'

    /* Libs for running tests */
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "com.android.support.test:runner:${androidTestLibVersion}"
    androidTestImplementation "com.android.support.test:rules:${androidTestLibVersion}"
    androidTestImplementation "com.android.support.test:runner:${androidTestLibVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${espressoLibVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${espressoLibVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:${espressoLibVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-idling-resource:${espressoLibVersion}"
    androidTestImplementation "com.android.support:support-annotations:${supportLibVersion}"

    /* For testing RecyclerViews */
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:2.2') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
}

//return a API key from a properties file.
def getPropertyFromFile(String property) {
    Properties properties = new Properties()
    properties.load(new FileInputStream("keys.properties"))
    return "\"" + properties.getProperty(property) +"\""
}
